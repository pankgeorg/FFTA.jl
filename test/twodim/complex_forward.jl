using FFTA, Test

@testset " forward. N=$N" for N in [8, 11, 15, 16, 27, 100]
    x = ones(ComplexF64, N, N)
    y = fft(x)
    y_ref = zero(y)
    y_ref[1] = length(x)
    @test y ≈ y_ref
    x = randn(N,N)
    @test fft(x) ≈ fft(reshape(x,1,N,N), [2,3])[1,:,:]
    @test fft(x) ≈ fft(reshape(x,1,N,N,1), [2,3])[1,:,:,1]
    @test fft(x) ≈ fft(reshape(x,1,1,N,N,1), [3,4])[1,1,:,:,1]
end

@testset "2D plan, 2D array. Size: $n" for n in 1:64
    @testset "size: ($m, $n)" for m in n:(n + 1)
        X = randn(ComplexF64, (m, n))

        @testset "against naive implementation" begin
            @test naive_2d_fourier_transform(X, FFTA.FFT_FORWARD) ≈ fft(X)
        end

        @testset "allocations" begin
            @test (@test_allocations fft(X)) <= 116 * Threads.nthreads()
        end
    end
end

@testset "$(N)D plan, $(N+1)D array" for N in 2:3
    rg = N == 2 ? (1:64) : (1:16)
    dims_lst = [[1,2], [1,3], [2,3]]
    if N == 3
        foreach(v -> push!(v, 4), dims_lst)
    end
    @testset "against $(N)D arrays with mapslices, r=$r" for r in dims_lst
        for n in rg
            x = randn(ComplexF64, ntuple(i -> n + (i - 1), N + 1))

            t = Tuple(r)    # test tuple region argument
            @test fft(x, t) == fft(x, r)
            # batched pencils round at ulp level differently from the
            # dims-subset path, like differently-shaped FFTW plans do
            @test fft(x, r) ≈ mapslices(fft, x; dims = r)
        end
    end
end

@testset "error messages" begin
    @test_throws DimensionMismatch fft(zeros(0, 0))
end
