# Plans

abstract type FFTAPlan{T,N} <: AbstractFFTs.Plan{T} end

struct FFTAInvPlan{_T,_N} <: FFTAPlan{_T,_N} end

const RegionTypes{N} = Union{Int,AbstractVector{Int},NTuple{N,Int}}

struct FFTAPlan_cx{T,N,R<:RegionTypes{N}} <: FFTAPlan{T,N}
    callgraph::NTuple{N,CallGraph{T}}
    region::R
    dir::Direction
    pinv::FFTAInvPlan{T,N}
end
function FFTAPlan_cx{T,N}(
    cg::NTuple{N,CallGraph{T}}, r::R,
    dir::Direction
) where {T,N,R<:RegionTypes{N}}
    FFTAPlan_cx{T,N,R}(cg, r, dir, FFTAInvPlan{T,N}())
end

struct FFTAPlan_re{T,N,R<:RegionTypes{N}} <: FFTAPlan{T,N}
    callgraph::NTuple{N,CallGraph{T}}
    region::R
    dir::Direction
    flen::Int
    pinv::FFTAInvPlan{T,N}
end
function FFTAPlan_re{T,N}(
    cg::NTuple{N,CallGraph{T}}, r::R,
    dir::Direction, flen::Int
) where {T,N,R<:RegionTypes{N}}
    FFTAPlan_re{T,N,R}(cg, r, dir, flen, FFTAInvPlan{T,N}())
end

function Base.size(p::FFTAPlan{<:Any,N}, i::Int) where N
    if i < 1
        throw(DomainError(i, "No non-positive dimensions"))
    elseif i > N
        1
    elseif p isa FFTAPlan_re && i == 1
        p.flen
    else
        first(p.callgraph[i].nodes).sz
    end
end
Base.size(p::FFTAPlan{<:Any,N}) where N = ntuple(Base.Fix1(size, p), Val{N}())

Base.complex(p::FFTAPlan_re{T,N,R}) where {T,N,R} = FFTAPlan_cx{T,N,R}(p.callgraph, p.region, p.dir, p.pinv)

function _sort(region::T)::T where {N,T<:NTuple{N,Int}}
    @static if VERSION >= v"1.12"
        sort(region)
    else
        if N == 2
            minmax(region[1], region[2])
        elseif N == 3
            t1, t2, t3 = region
            t1, t2 = minmax(t1, t2)
            t2, t3 = minmax(t2, t3)
            t1, t2 = minmax(t1, t2)
            (t1, t2, t3)
        else
            NTuple{N}(sort!(collect(region)))
        end
    end
end

_sort(region::T) where T<:RegionTypes = issorted(region) ? copy(region) : sort(region)

AbstractFFTs.plan_fft(x::AbstractArray{T,N}, region; kwargs...) where {T<:Complex,N} =
    _plan_fft(x, region, FFT_FORWARD; kwargs...)

AbstractFFTs.plan_bfft(x::AbstractArray{T,N}, region; kwargs...) where {T<:Complex,N} =
    _plan_fft(x, region, FFT_BACKWARD; kwargs...)

function _plan_fft(
    x::AbstractArray{T,N},
    region::RegionTypes,
    dir::Direction;
    BLUESTEIN_CUTOFF=DEFAULT_BLUESTEIN_CUTOFF, _kwargs...
) where {T<:Complex,N}
    M = length(region)
    if M == 1
        R1 = Int(region[1])
        g = CallGraph{T}(size(x, R1), BLUESTEIN_CUTOFF, dir)
        return FFTAPlan_cx{T,1}((g,), R1, dir)
    elseif M == 2
        R2 = _sort(region)
        g1 = CallGraph{T}(size(x, R2[1]), BLUESTEIN_CUTOFF, dir)
        g2 = CallGraph{T}(size(x, R2[2]), BLUESTEIN_CUTOFF, dir)
        return FFTAPlan_cx{T,2}((g1, g2), R2, dir)
    else
        RM = _sort(region)
        return FFTAPlan_cx{T,M}(
            ntuple(i -> CallGraph{T}(size(x, RM[i]), BLUESTEIN_CUTOFF, dir), Val(M)),
            RM, dir
        )
    end
end

function AbstractFFTs.plan_rfft(
    x::AbstractArray{T,N},
    region::RegionTypes;
    BLUESTEIN_CUTOFF=DEFAULT_BLUESTEIN_CUTOFF, _kwargs...
) where {T<:Real,N}
    M = length(region)
    if M == 1
        R1 = Int(region[1])
        n = size(x, R1)
        # For even length problems, we solve the real problem with
        # two n/2 complex FFTs followed by a butterfly. For odd size
        # problems, we just solve the problem as a single complex
        nn = iseven(n) ? n >> 1 : n
        g = CallGraph{Complex{T}}(nn, BLUESTEIN_CUTOFF, FFT_FORWARD)
        return FFTAPlan_re{Complex{T},1}((g,), R1, FFT_FORWARD, n)
    elseif M == 2
        R2 = _sort(region)
        g1 = CallGraph{Complex{T}}(size(x, R2[1]), BLUESTEIN_CUTOFF, FFT_FORWARD)
        g2 = CallGraph{Complex{T}}(size(x, R2[2]), BLUESTEIN_CUTOFF, FFT_FORWARD)
        return FFTAPlan_re{Complex{T},2}((g1, g2), R2, FFT_FORWARD, size(x, R2[1]))
    else
        throw(ArgumentError("only supports 1D and 2D FFTs"))
    end
end

function AbstractFFTs.plan_brfft(
    x::AbstractArray{T,N},
    len::Int,
    region::RegionTypes;
    BLUESTEIN_CUTOFF=DEFAULT_BLUESTEIN_CUTOFF, _kwargs...
) where {T,N}
    M = length(region)
    if M == 1
        # For even length problems, we solve the real problem with
        # two n/2 complex FFTs followed by a butterfly. For odd size
        # problems, we just solve the problem as a single complex
        R1 = Int(region[1])
        nn = iseven(len) ? len >> 1 : len
        g = CallGraph{T}(nn, BLUESTEIN_CUTOFF, FFT_BACKWARD)
        return FFTAPlan_re{T,1}((g,), R1, FFT_BACKWARD, len)
    elseif M == 2
        R2 = _sort(region)
        g1 = CallGraph{T}(len, BLUESTEIN_CUTOFF, FFT_BACKWARD)
        g2 = CallGraph{T}(size(x, R2[2]), BLUESTEIN_CUTOFF, FFT_BACKWARD)
        return FFTAPlan_re{T,2}((g1, g2), R2, FFT_BACKWARD, len)
    else
        throw(ArgumentError("only supports 1D and 2D FFTs"))
    end
end


# Multiplication
## mul!
### Complex
#### 1D plan 1D array
function LinearAlgebra.mul!(y::AbstractVector{U}, p::FFTAPlan_cx{T,1}, x::AbstractVector{T}) where {T,U}
    Base.require_one_based_indexing(x, y)
    if axes(x) != axes(y)
        throw(DimensionMismatch("input array has axes $(axes(x)), but output array has axes $(axes(y))"))
    end
    if size(p) != size(x)
        throw(DimensionMismatch("plan has axes $(size(p)), but input array has axes $(size(x))"))
    end
    fft!(y, x, 1, 1, p.dir, p.callgraph[1][1].type, p.callgraph[1], 1)
    return y
end

#### 1D plan ND array
function LinearAlgebra.mul!(y::AbstractArray{U,N}, p::FFTAPlan_cx{T,1}, x::AbstractArray{T,N}) where {T,U,N}
    Base.require_one_based_indexing(x, y)

    ax_x, ax_y = axes(x), axes(y)
    if ax_x != ax_y
        throw(DimensionMismatch("input array has axes $ax_x, but output array has axes $ax_y"))
    end

    R1 = only(p.region)
    plen, xlen = size(p, 1), size(x, R1)
    if plen != xlen
        throw(DimensionMismatch("plan has size $plen, but input array has size $xlen along region $R1"))
    end

    if @generated
        quote
            Base.Cartesian.@nif $N d -> (d == R1) dim -> (_mul_loop!(y, x, p, Val(dim)))
        end
    else
        _mul_loop!(y, x, p, Val(R1))
    end
    return y
end

function _mul_loop!(
    y::AbstractArray{U,N},
    x::AbstractArray{T,N},
    p::FFTAPlan_cx{T,1},
    ::Val{R}
) where {T,U,N,R}
    Rpre  = CartesianIndices(ntuple(Base.Fix1(size, x),  Val(R - 1)))
    Rpost = CartesianIndices(ntuple(i -> size(x, R + i), Val(N - R)))
    cg = p.callgraph[1]
    t = cg[1].type
    for Ipost in Rpost, Ipre in Rpre
        @views fft!(y[Ipre,:,Ipost], x[Ipre,:,Ipost], 1, 1, p.dir, t, cg, 1)
    end
end

#### ND plan ND array
function LinearAlgebra.mul!(
    out::AbstractArray{U,N},
    p::FFTAPlan_cx{T,N},
    X::AbstractArray{T,N}
) where {T,U,N}
    Base.require_one_based_indexing(out, X)
    if size(out) != size(X)
        throw(DimensionMismatch("input array has axes $(axes(X)), but output array has axes $(axes(out))"))
    elseif size(p) != size(X)
        throw(DimensionMismatch("plan has size $(size(p)), but input array has size $(size(X))"))
    elseif !region_isvalid(p.region, N)
        throw(DimensionMismatch("Plan region is outside array dimensions."))
    end

    sz = size(X)
    max_sz = maximum(sz)
    obuf = Vector{T}(undef, max_sz)
    ibuf = Vector{T}(undef, max_sz)
    sizehint!(obuf, max_sz) # not guaranteed but hopefully prevents allocations
    sizehint!(ibuf, max_sz)
    dir = p.dir

    copyto!(out, X) # operate in-place on output array

    if @generated
        quote
            Base.Cartesian.@nexprs $N dim -> begin
                n = size(out, dim)
                resize!(obuf, n)
                resize!(ibuf, n)
                cg = p.callgraph[dim]

                fft_along_dim!(out, ibuf, obuf, cg, dir, Val(dim))
            end
        end
    else
        for dim in 1:N
            n = size(out, dim)
            resize!(obuf, n)
            resize!(ibuf, n)
            cg = p.callgraph[dim]

            fft_along_dim!(out, ibuf, obuf, cg, dir, Val(dim))
        end
    end

    return out
end

#### MD plan ND array (M<N)
function LinearAlgebra.mul!(
    out::AbstractArray{U,N},
    p::FFTAPlan_cx{T,M},
    X::AbstractArray{T,N}
) where {T,U,N,M}
    Base.require_one_based_indexing(out, X)
    if size(out) != size(X)
        throw(DimensionMismatch("input array has axes $(axes(X)), but output array has axes $(axes(out))"))
    elseif !region_isvalid(p.region, M, N)
        throw(DimensionMismatch("Region is invalid."))
    elseif M > N || first(p.region) < 1 || last(p.region) > N
        throw(DimensionMismatch("Plan region is outside array dimensions."))
    end

    max_sz = maximum(Base.Fix1(size, out), p.region)
    obuf = Vector{T}(undef, max_sz)
    ibuf = Vector{T}(undef, max_sz)
    sizehint!(obuf, max_sz) # not guaranteed but hopefully prevents allocations
    sizehint!(ibuf, max_sz)

    copyto!(out, X) # operate in-place on output array

    _execute_mdfft!(out, ibuf, obuf, p.dir, p.region, p.callgraph)

    return out
end

@noinline function _execute_mdfft!(
    out::AbstractArray{U,N},
    ibuf::Vector{T}, obuf::Vector{T},
    dir::Direction,
    @nospecialize(region::RegionTypes),
    @nospecialize(callgraphs::NTuple)
) where {T,U,N}

    M = length(region)
    if @generated
        quote
            k = 1
            # region is assumed to be pre-sorted during planning
            Base.Cartesian.@nexprs $N dim -> begin
                if region[k] == dim
                    n = size(out, dim)
                    resize!(obuf, n)
                    resize!(ibuf, n)
                    cg = callgraphs[k]

                    fft_along_dim!(out, ibuf, obuf, cg, dir, Val(dim))

                    k = min(k + 1, M)
                end
            end
            return nothing
        end
    else
        for dim in 1:M
            pdim = region[dim]
            n = size(out, pdim)
            resize!(obuf, n)
            resize!(ibuf, n)
            cg = callgraphs[dim]

            fft_along_dim!(out, ibuf, obuf, cg, dir, Val(pdim))
        end
    end
end

function fft_along_dim!(
    A::AbstractArray{U,N},
    ibuf::Vector{T}, obuf::Vector{T},
    cg::CallGraph{T}, d::Direction,
    ::Val{dim}
) where {T <: Complex{<:AbstractFloat}, U, N, dim}

    Rpre  = CartesianIndices(ntuple(Base.Fix1(size, A),    Val(dim - 1)))
    Rpost = CartesianIndices(ntuple(i -> size(A, dim + i), Val(N - dim)))
    t = cg[1].type
    cols = eachindex(axes(A, dim), ibuf, obuf)

    for Ipost in Rpost, Ipre in Rpre
        for j in cols
            ibuf[j] = A[Ipre, j, Ipost]
        end
        fft!(obuf, ibuf, 1, 1, d, t, cg, 1)
        for j in cols
            A[Ipre, j, Ipost] = obuf[j]
        end
    end
end

region_isvalid(r::Int, N::Int, _::Int=0) = r == N == 1
region_isvalid(r::AbstractVector{Int}, N::Int) = r == 1:N
region_isvalid(r::AbstractRange{Int}, M::Int, _::Int) = issorted(r) && length(r) == M
function region_isvalid(r::NTuple{M,Int}, N::Int) where M
    isvalid = M == N
    for i in 1:M
        isvalid &= (r[i] == i)
    end
    isvalid
end
function region_isvalid(r::Union{AbstractVector{Int},NTuple{<:Any,Int}}, M::Int, _::Int)
    isvalid = length(r) == M
    maybe_p = Iterators.peel(r)
    isnothing(maybe_p) && return isvalid
    p, rest = maybe_p
    for n in rest
        isvalid = isvalid && (p < n)
        p = n
    end
    isvalid
end

## *
### Complex
function Base.:*(p::FFTAPlan_cx{T,1}, x::AbstractVector{T}) where {T<:Complex}
    y = similar(x)
    LinearAlgebra.mul!(y, p, x)
    y
end

function Base.:*(p::FFTAPlan_cx{T,N1}, x::AbstractArray{T,N2}) where {T<:Complex,N1,N2}
    y = similar(x)
    LinearAlgebra.mul!(y, p, x)
    y
end

### Real
# By converting the problem to complex and back to real
#### 1D plan 1D array
##### Forward
function Base.:*(p::FFTAPlan_re{Complex{T},1}, x::AbstractVector{T}) where {T<:Real}
    if p.dir !== FFT_FORWARD
        throw(ArgumentError("only FFT_FORWARD supported for real vectors"))
    end
    Base.require_one_based_indexing(x)

    n = p.flen
    p_c = complex(p)
    if iseven(n)
        # For problems of even size, we solve the rfft problem by splitting the
        # problem into the even and odd part and solving them simultaneously as
        # a single (complex) fft of half the size, see equations (6)-(8) of
        # Sorensen, H. V., D. Jones, Michael Heideman, and C. Burrus.
        # "Real-valued fast Fourier transform algorithms."
        # IEEE Transactions on acoustics, speech, and signal processing 35, no. 6 (2003): 849-863.
        if x isa Vector && isbitstype(T)
            # For a vector of bits, we can just reinterpret the bits to get the
            # appropriate representation of even (zero based) elements as the real
            # part and the odd as the complex part
            x_c = reinterpret(Complex{T}, x)
        else
            # for non-bits, we'd have to copy to a new array
            x_c = complex.(view(x, 1:2:n), view(x, 2:2:n))
        end

        m = n >> 1
        # Allocate complex result vector of half the input size plus one
        y = similar(x_c, m + 1)
        # Solve the complex fft of half the size
        LinearAlgebra.mul!(view(y, 1:m), p_c, x_c)

        # The w stored in the plan is for m, not n, so probably cheapest to
        # just recompute it instead of taking a square root
        z1 = singleton_params(-one(T) / n)
        wj = cispi(-T(2) / n)

        # Construct the result by first constructing the elements of the
        # real and imaginary part, followed by the usual radix-2 assembly,
        # see eq (9)
        y1     = y[1]
        y[1]   = real(y1) + imag(y1)
        y[end] = real(y1) - imag(y1)

        @inbounds for j in 2:((m >> 1) + 1)
            yj  = y[j]
            ymj = y[m-j+2]
            XX = T(0.5) * ( yj + conj(ymj))
            XY = T(0.5) * (-yj + conj(ymj)) * im
            y[j]     =      XX + wj * XY
            y[m-j+2] = conj(XX - wj * XY)
            wj = singleton_step(wj, z1)
        end
        return y
    else
        # when the problem cannot be split in two equal size chunks we
        # convert the problem to a complex fft and truncate the redundant
        # part of the result vector
        if size(p_c) != size(x)
            throw(DimensionMismatch("plan and input array axes do not match"))
        end
        y = similar(x, Complex{T})
        fft!(y, x, 1, 1, p_c.dir, p_c.callgraph[1][1].type, p_c.callgraph[1], 1)
        return y[1:end÷2+1]
    end
end

##### Backward
function Base.:*(p::FFTAPlan_re{T,1}, x::AbstractVector{T}) where {T<:Complex}
    if p.dir !== FFT_BACKWARD
        throw(ArgumentError("only FFT_BACKWARD supported for complex vectors"))
    end
    Base.require_one_based_indexing(x)

    n = p.flen
    p_c = complex(p)
    # See explanation of this approach in the method for the FORWARD transform
    if iseven(n)
        m = n >> 1

        R = real(T)
        z1 = singleton_params(one(R) / n)
        wj = cispi(R(2) / n)

        x_tmp = similar(x, length(x) - 1)
        x_tmp[1] = complex(
            (real(x[1]) + real(x[end])),
            (real(x[1]) - real(x[end]))
        )
        for j in 2:((m >> 1) + 1)
            XX =       x[j] + conj(x[m-j+2])
            XY = wj * (x[j] - conj(x[m-j+2]))
            x_tmp[j]     =      XX + im * XY
            x_tmp[m-j+2] = conj(XX - im * XY)
            wj = singleton_step(wj, z1)
        end

        y_c = p_c * x_tmp
        if isbitstype(T)
            return copy(reinterpret(R, y_c))
        else
            y_re = similar(y_c, R, 2 * length(y_c))
            for i in eachindex(y_c)
                y_re[2i-1], y_re[2i] = reim(y_c[i])
            end
            return y_re
        end
    else
        x_tmp = similar(x, n)
        x_tmp[1:end÷2+1] .= x
        x_tmp[end÷2+2:end] .= @views conj.(x[end-iseven(n):-1:2])
        y = similar(x_tmp)
        LinearAlgebra.mul!(y, p_c, x_tmp)
        return real(y)
    end
end

#### 1D plan ND array
##### Forward
function Base.:*(p::FFTAPlan_re{Complex{T},1}, x::AbstractArray{T,N}) where {T<:Real,N}
    if p.dir !== FFT_FORWARD
        throw(ArgumentError("only FFT_FORWARD supported for real arrays"))
    end
    Base.require_one_based_indexing(x)
    return mapslices(Base.Fix1(*, p), x; dims=only(p.region))
end

##### Backward
function Base.:*(p::FFTAPlan_re{T,1}, x::AbstractArray{T,N}) where {T<:Complex,N}
    if p.dir !== FFT_BACKWARD
        throw(ArgumentError("only FFT_BACKWARD supported for complex arrays"))
    end
    Base.require_one_based_indexing(x)
    dim1 = only(p.region)
    rlen = p.flen ÷ 2 + 1
    if rlen != size(x, dim1)
        throw(DimensionMismatch("real 1D plan has size $(p.flen). Dimension of input array along region $dim1 should have size $rlen, but has size $(size(x, dim1))"))
    end
    return mapslices(Base.Fix1(*, p), x; dims=dim1)
end

#### 2D plan ND array
##### Forward
function Base.:*(p::FFTAPlan_re{Complex{T},2}, x::AbstractArray{T,N}) where {T<:Real,N}
    if p.dir !== FFT_FORWARD
        throw(ArgumentError("only FFT_FORWARD supported for real arrays"))
    end
    Base.require_one_based_indexing(x)
    half_1 = 1:(p.flen÷2+1)
    x_c = complex(x)
    y = similar(x_c)
    LinearAlgebra.mul!(y, complex(p), x_c)
    return copy(selectdim(y, first(p.region), half_1))
end

##### Backward
function Base.:*(p::FFTAPlan_re{T,2}, x::AbstractArray{T,N}) where {T<:Complex,N}
    if p.dir !== FFT_BACKWARD
        throw(ArgumentError("only FFT_BACKWARD supported for complex arrays"))
    end
    Base.require_one_based_indexing(x)

    dim1 = first(p.region)
    dim2 = last(p.region)
    x_sz = (xrows, xcols) = (size(x, dim1), size(x, dim2))

    flen = p.flen
    tlen = flen ÷ 2 + 1
    t_sz = (tlen, size(p, 2))

    if t_sz != x_sz
        throw(DimensionMismatch("real 2D plan has size $(size(p)). Transform dimensions of input array are $x_sz but should be $t_sz"))
    end

    res_size = ntuple(i -> ifelse(i == dim1, flen, size(x, i)), Val(N))
    # for the inverse transformation we have to reconstruct the full array
    half_1 = 1:tlen
    half_2 = tlen+1:flen
    x_full = similar(x, res_size)
    # use first half as is
    copy!(selectdim(x_full, dim1, half_1), x)

    # the second half in the first transform dimension is reversed and conjugated
    x_half_2 = selectdim(x_full, dim1, half_2) # view to the second half of x
    start_reverse = xrows - iseven(flen)

    map!(conj, x_half_2, selectdim(x, dim1, start_reverse:-1:2))
    # for the 2D transform we have to reverse index 2:end of the same block in the second transform dimension as well
    reverse!(selectdim(x_half_2, dim2, 2:xcols), dims=dim2)

    y = similar(x_full)
    LinearAlgebra.mul!(y, complex(p), x_full)

    return real(y)
end
